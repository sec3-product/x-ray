; Script generated by the HM NIS Edit Script Wizard.
; Install log: https://nsis.sourceforge.io/Uninstall_from_NSIS_log
; Special build log: https://sourceforge.net/projects/nsis/files/NSIS%203/3.06.1/
; enVar plugin: https://nsis.sourceforge.io/EnVar_plug-in
; 

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Coderrect"
; !define PRODUCT_VERSION "0.9.3"
!define PRODUCT_PUBLISHER "Coderrect, Inc."
!define PRODUCT_WEB_SITE "http://www.coderrect.com"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\coderrect\coderrect.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

Unicode true

; MUI 1.67 compatible ------
!include "MUI2.nsh"
!include x64.nsh

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "nsis\main.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "nsis\EULA.txt"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; 
!include "nsis\addpath.nsdinc"
Page custom fnc_AddPathPage_Show fnc_AddPathPage_Leave

; Finish page
!define MUI_PAGE_CUSTOMFUNCTION_SHOW FinishPageShow
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

!include uninstallfromlog-unicode.nsh

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "Setup.exe"
InstallDir "$PROGRAMFILES64\Coderrect"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Section "MainSection" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer

  ; file
  LogSet On
  File /nonfatal /a /r "build\"
  LogSet Off

  ; menu
  CreateDirectory "$SMPROGRAMS\Coderrect"
  CreateShortCut "$SMPROGRAMS\Coderrect\coderrect.lnk" "$INSTDIR\coderrect.exe"
  CreateShortCut "$DESKTOP\coderrect.lnk" "$INSTDIR\coderrect.exe"
SectionEnd

Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\Coderrect\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\Coderrect\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\coderrect.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\coderrect.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


var IsAddPath

Function .onInit
  StrCpy $IsAddPath "1"  
FunctionEnd

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "Coderrect uninstall from your computer successfully!"
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure to uninstall Coderrect?" IDYES +2
  Abort
FunctionEnd

Function FinishPageShow
  ; back button
  GetDlgItem $0 $HWNDPARENT 3
  ShowWindow $0 ${SW_HIDE}

  ; back button
  GetDlgItem $0 $HWNDPARENT 2
  ShowWindow $0 ${SW_HIDE}
FunctionEnd

; --------------------------------------------------------------
; Uninstall Sections
; --------------------------------------------------------------
Section Uninstall

  Call un.CreateLogFromFile
  Call un.RemoveDirectoriesFromLog
  Delete "$INSTDIR\install.log"

  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"

  Delete "$SMPROGRAMS\Coderrect\Uninstall.lnk"
  Delete "$SMPROGRAMS\Coderrect\Website.lnk"
  Delete "$DESKTOP\coderrect.lnk"
  Delete "$SMPROGRAMS\Coderrect\coderrect.lnk"

  RMDir "$SMPROGRAMS\Coderrect"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  ; SetAutoClose true
SectionEnd

; --------------------------------------------------------------
; add path Sections
; --------------------------------------------------------------
Function addPathOnClicked
  ${If} $IsAddPath == "0"
    StrCpy $IsAddPath "1"  
  ${Else}
    StrCpy $IsAddPath "0"
  ${EndIf}
FunctionEnd

Function fnc_AddPathPage_Leave
  ${If} $IsAddPath == "0"
    Return
  ${EndIf}

  EnVar::SetHKLM
  ; Add an expanded value
  EnVar::AddValue "Path" "$INSTDIR\bin"
FunctionEnd